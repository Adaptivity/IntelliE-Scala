apply plugin: 'java'        // enables sourcesets
apply plugin: 'scala'       // enable scala
apply plugin: 'forge'       // forge tasks

apply from: 'gradle/scripts/properties.gradle'
apply from: 'gradle/scripts/versioning.gradle'
apply from: 'gradle/scripts/curse.gradle'
apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/javacompat.gradle'
apply from: 'gradle/scripts/idea.gradle'
apply from: 'gradle/scripts/artifacts.gradle'

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

// needed for pulling new versions from forge
minecraft {
    version = "${VERSION_MINECRAFT}-10.13.1.1216-new"
    replace "@version@", VERSION_PROJECT.toString()
    runDir = "run"
}

sourceSets {
    main {
        scala {
            srcDirs += "src/api/scala"
        }
    }
}

// uses scala compile daemon
compileScala {
    scalaCompileOptions.useCompileDaemon = true
}

// use FSC and not Ant to compile
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}



processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", VERSION_PROJECT.toString()
    inputs.property "mcversion", VERSION_MINECRAFT

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': VERSION_PROJECT.toString(), 'mcversion': VERSION_MINECRAFT
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}