apply plugin: 'forge'   // forge tasks
apply plugin: 'java'    // enables sourcesets
apply plugin: 'idea'    // enable automatic setup of idea workspace
apply plugin: 'scala'   // enable scala

apply from: 'gradle/scripts/properties.gradle'
apply from: 'gradle/scripts/versioning.gradle'

sourceCompatibility = VERSION_JAVA
targetCompatibility = VERSION_JAVA

// needed for pulling new versions from forge
minecraft {
	version = "${VERSION_MINECRAFT}-${VERSION_FORGE}"
	assetDir = "out/assets"
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

// exclude from AE2 api
sourceSets.api.java {
	exclude '**/layers/**'
	exclude '**/integration/**'
}

dependencies {
	compile "com.google.code.findbugs:jsr305:${VERSION_FINDBUGS}"

	testCompile "junit:junit:${VERSION_JUNIT}"
	testCompile "org.scalatest:scalatest_2.10:${VERSION_SCALATEST}"
	testCompile "org.scalamock:scalamock_2.10:${VERSION_SCALAMOCK}"
}

// resulting file with [baseName]-[appendix]-[version]-[classifier].[extension]
jar {
	baseName = "IntelligentEnergistics"
	appendix = VERSION_MINECRAFT
	version = VERSION_PROJECT.toString()
	classifier = "universal"
}

// disable Ant from scala compiling
tasks.withType(ScalaCompile) {
	scalaCompileOptions.useAnt = false
}

processResources {
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': VERSION_PROJECT.toString(), 'mcversion': VERSION_MINECRAFT
	}
}
