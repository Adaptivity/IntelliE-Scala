apply plugin: 'java'        // enables sourcesets
apply plugin: 'scala'       // enable scala
apply plugin: 'forge'       // forge tasks
apply plugin: 'idea'        // enable automatic setup of idea workspace
apply plugin: 'curseforge'  // enable automatic upload to curseforge

apply from: 'gradle/scripts/properties.gradle'
apply from: 'gradle/scripts/versioning.gradle'
apply from: 'gradle/scripts/curse.gradle'
//apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/javacompat.gradle'

// needed for pulling new versions from forge
minecraft {
    version = "${VERSION_MINECRAFT}-${VERSION_FORGE}"
    runDir = "run"
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

repositories {
    mavenCentral()
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
}

dependencies {
    compile "appeng:appliedenergistics2:rv1-beta-4:dev"

    compile "mcp.mobius.waila:Waila:1.5.3_1.7.10"
    // These will be included automatically
    //compile "codechicken:NotEnoughItems:1.7.10-1.0.2.31:dev"
    //compile "codechicken:CodeChickenLib:1.7.10-1.1.1.95:dev"
    //compile "codechicken:CodeChickenCore:1.7.10-1.0.2.11:dev"

    testCompile "org.scalatest:scalatest_2.11:2.2.0"
    testCompile "org.scalamock:scalamock-scalatest-support_2.11:3.1.2"
    testCompile "org.scalamock:scalamock-specs2-support_2.11:3.1.2'"
//	testCompile "org.scalatest:scalatest_2.10:${VERSION_SCALATEST}"
//	testCompile "org.scalamock:scalamock_2.10:${VERSION_SCALAMOCK}"
}

// resulting file with [baseName]-[appendix]-[version]-[classifier].[extension]
jar {
    baseName = "IntelligentEnergistics"
    appendix = VERSION_MINECRAFT
    version = VERSION_PROJECT.toString()
    classifier = "universal"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", VERSION_PROJECT.toString()
    inputs.property "mcversion", VERSION_MINECRAFT

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': VERSION_PROJECT.toString(), 'mcversion': VERSION_MINECRAFT
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}