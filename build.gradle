apply plugin: 'forge'   // forge tasks
apply plugin: 'java'    // enables sourcesets
apply plugin: 'idea'    // enable automatic setup of idea workspace
apply plugin: 'scala'   // enable scala

apply from: 'gradle/scripts/versioning.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'de.thatsich.intellie'
archivesBaseName = "IntelligentEnergistics"
version = project.ext.version.toString()

// needed for pulling new versions from forge
minecraft {
	version = "1.7.2-10.12.0.1057"
	assetDir = "out/assets"
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

dependencies {
	compile "com.google.code.findbugs:jsr305:2.0.3"
}

// exclude layers and integration of (AE2)
sourceSets {
	api {
		java {
			exclude '**/layers/**'
			exclude '**/integration/**'
		}
	}
}

// resulting file with <archivesBaseName>-<version>-<classifier>
jar {
	classifier = "universal"
	version = "${project.minecraft.version}-${project.version}"
}

// disable Ant from scala compiling
tasks.withType(ScalaCompile) {
	scalaCompileOptions.useAnt = false
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
