def theLastGitTag = getLastGitTag()
def theChangelog = getChangelog()

curse {
	apiKey = "eb5b9b11-5d6a-405c-baa2-3acac200baee"
	projectId = "222848"
	changelog = theChangelog
	releaseType = "alpha"
}

private String getChangelog() {
	def outStream = new ByteArrayOutputStream()
	String changelog = ""
	String lastGitTag = getLastGitTag()

	try {
		exec {
			executable = "git"
			args = ["log", "--pretty=%B", "$lastGitTag..HEAD"]
			standardOutput = outStream
		}

		changelog = outStream.toString().trim()
	}
	catch (e) {
		logger.quiet "Error while generating theChangelog: $e"
	}

	return changelog
}

private String getLastGitTag() {
	def outStream = new ByteArrayOutputStream()
	def lastGitTag = ""

	try {
		exec {
			executable = "git"
			args = ["describe", "--abbrev=0"]
			standardOutput = outStream
		}

		lastGitTag = outStream.toString().trim()
	}
	catch (e) {
		logger.quiet "Error while fetching last git tag: $e"
	}

	return lastGitTag
}

task printChangelog(group: 'Debug', description: 'Prints the theChangelog for debugging purposes.') << {
	logger.quiet "The last changelog:\n\n$theChangelog"
}

task printLastGitTag(group: 'Debug', description: 'Prints the last git tag for debugging purposes.') << {
	logger.quiet "The last git tag: $theLastGitTag"
}